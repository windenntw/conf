source ~/.yacoob-conf

# few terminal keybinds
bindkey -e
zle -N fake-enter; bindkey "^X^H" fake-enter

zmodload -a colors
zmodload -a complist

# aliases
alias mv='nocorrect mv -i'
alias cp='nocorrect cp -i'
alias rm='rm -i'
alias mkdir='nocorrect mkdir'

alias j=jobs
alias pu=pushd
alias po=popd
alias d='dirs -v'
alias h=history
alias grep=egrep

alias ll='ls -lh'
alias lld='ls -ld *(-/DN)'
alias la='ls -A'
alias lla='ll -A'

alias cd..='cd ..'
alias cd...='cd ../..'
alias cd/='cd /'

alias dlb='rm  -i (.*~|*~)'
alias dlbf='rm -f (.*~|*~)'
alias pwg='pwgen -c -n -y -s -B 12 1'
alias rdlbf='find . -iname \*~ | xargs rm -f'
alias sl='screen -list'

autoload run-help

# global aliases
alias -g L='|less'
alias -g H='|head'
alias -g T='|tail'
alias -g G="egrep"

# zsh parameters
typeset -U path

cdpath=(. ~)
DIRSTACKSIZE=20

# Autoload zsh functions, including site-specific
fpath=(~/.zsh/functions* $fpath)
autoload -U ~/.zsh/functions*/*(:t)

fignore=(\~)

LISTMAX=0
LOGCHECK=60
HISTSIZE=20000
HISTFILE=~/.zsh_history
MAILCHECK=1
READNULLCMD=less
REPORTTIME=15
SAVEHIST=20000
TIMEFMT='%J  %*U user %*S system %P cpu (%*E wasted time).'
watch=(all)
WATCHFMT='%n %a %l from %m at %t.'
WORDCHARS="${WORDCHARS:s#/#}"


# zsh options
setopt \
  auto_cd \
  auto_name_dirs \
  auto_pushd \
  auto_resume \
  nobeep \
  cdable_vars \
  csh_null_glob \
  correct \
  correct_all \
  extended_glob \
  NO_glob_dots \
  hist_allow_clobber \
  hist_find_no_dups \
  hist_ignore_all_dups \
  hist_ignore_dups \
  hist_ignore_space \
  hist_reduce_blanks \
  HIST_save_no_dups \
  inc_append_history \
  NO_list_ambiguous \
  NO_list_beep \
  long_list_jobs \
  magic_equal_subst \
  notify \
  prompt_subst \
  pushd_minus \
  pushd_silent \
  pushd_to_home \
  rc_quotes \
  share_history \
  shwordsplit \
  transient_rprompt \

# Modules
autoload -U url-quote-magic
zle -N self-insert url-quote-magic
autoload -U zmv
alias zmv='noglob zmv'
alias mmv='noglob zmv -W'

# Enable auto-execution of functions.
typeset -ga preexec_functions
typeset -ga precmd_functions
typeset -ga periodic_functions

# VCS info for right side prompt
autoload -Uz vcs_info
zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:*' stagedstr '%F{yellow}●'
zstyle ':vcs_info:*' unstagedstr '%F{red}●'
zstyle ':vcs_info:*' formats '%u%c %F{green}%b %F{default}'
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' get-revision  true
precmd_functions+='vcs_info'

# Terminal title
preexec_functions+='preexec_term_title'
precmd_functions+='precmd_term_title'

# Conf dir updates
PERIOD=300
periodic_functions+='conf-prompt-helper'

# Prompt definition.
# Anonymous function is used to isolate local variables from user environment.
function {
  local -a plines tmp

  # First line.
  tmp+='%(!.%F{red}✖%F{default}.%F{green}❤%F{default})'
  tmp+=' [%(2L.+.)%n${SSH_CONNECTION:+"%F{red}@%F{default}%m}"%F{default}]'
  tmp+=' [%F{red}%T %w%F{default}]'
  tmp+=' [%F{yellow}%!%F{default}]'
  tmp+=' [%(?.-.%F{cyan}%?%F{default})]'
  plines+=${(j::)tmp}

  # Second line.
  plines+='[%F{cyan}%~%F{default}]'

  # Third line.
  tmp=()
  tmp+='%F{cyan}${_CONF_UPDATE:+⟳  }%F{default}'
  tmp+='%(?,,%F{red})%(!.#.$)%F{default} '
  plines+=${(j::)tmp}

  # PROMPT, assemble!
  PROMPT=${(F)plines}
 }
RPROMPT=$'${vcs_info_msg_0_}'


# Umask
umask 027

# System/location dependent settings
case "${OS}" in
    "linux")
        alias ls='ls --color=auto'
        ;;
    "osx")
        alias ls='ls -G'
        ;;
esac

# Tab-completion
autoload -Uz compinit
[[ -d "${ZDOTDIR:-$HOME}/.zcompdumps" ]] || mkdir -m 0700 -p "${ZDOTDIR:-$HOME}/.zcompdumps"
compinit -i -d "${ZDOTDIR:-$HOME}/.zcompdumps/${HOST%%.*}-$ZSH_VERSION"

# Bring in bash compability layer for tabcompletions
autoload -Uz bashcompinit
bashcompinit

zstyle ':completion::complete:cd::' tag-order '! users' -
zstyle ':completion::complete:-command-::' tag-order '! users' -
compdef g4=p4

zstyle ':completion:*:sudo:*' command-path /usr/local/sbin /usr/local/bin \
     /usr/sbin /usr/bin /sbin /bin /usr/X11R6/bin
zstyle -e ':completion:*:approximate:*' max-errors 'reply=( $(( ($#PREFIX + $#SUFFIX) / 3 )) )'
zstyle ':completion:*:descriptions' format "- %d -"
zstyle ':completion:*:corrections' format "- %d - (errors %e})"
zstyle ':completion:*:default' list-prompt '%S%M matches%s'
zstyle ':completion:*' group-name ''
zstyle ':completion:*:manuals' separate-sections true
zstyle ':completion:*:manuals.(^1*)' insert-sections true
zstyle ':completion:*' menu select
zstyle ':completion:*' verbose yes
zstyle ':completion:*:kill:*:processes' command "ps x"
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*' list-colors 'reply=( "=(#b)(*$VAR)(?)*=00=$color[green]=$color[bg-green]" )'
zstyle ':completion:*:*:*:*:hosts' list-colors '=*=30;41'
zstyle ':completion:*:*:*:*:users' list-colors '=*=$color[green]=$color[red]'
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path ~/.zsh/cache

# Location dependent confidential settings
[[ -r ~/.zshrc.${LOCATION} ]] && source ~/.zshrc.${LOCATION}

# A dummy command that returns positive exit status
:
